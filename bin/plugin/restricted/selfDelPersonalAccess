#! /usr/bin/env perl
# vim: set filetype=perl ts=4 sw=4 sts=4 et:
use common::sense;

use File::Basename;
use lib dirname(__FILE__) . '/../../../lib/perl';
use OVH::Result;
use OVH::Bastion;
use OVH::Bastion::Plugin qw( :DEFAULT help );
use OVH::Bastion::Plugin::ACL;

my $remainingOptions = OVH::Bastion::Plugin::begin(
    argv               => \@ARGV,
    header             => "removing personal access to a server from an account",
    userAllowWildcards => 1,
    options            => {
        "protocol=s"   => \my $protocol,
        "proxy-host=s" => \my $proxyHost,
        "proxy-port=s" => \my $proxyPort,
        # undocumented/compatibility:
        "user-any" => \my $userAny,
        "port-any" => \my $portAny,
        "scpup"    => \my $scpUp,
        "scpdown"  => \my $scpDown,
        "sftp"     => \my $sftp,
        "proxy-user=s"     => \my $proxyUser,
    },
    helptext => <<'EOF',
Remove a personal server access from your account

Usage: --osh SCRIPT_NAME --host HOST --user USER --port PORT [OPTIONS]

  --host HOST|IP|SUBNET    Host(s) to remove access from, either a HOST which will be resolved to an IP immediately,
                             or an IP, or a whole subnet using the PREFIX/SIZE notation
  --user USER|PATTERN|*    Specify which remote user was allowed to connect as.
                             Globbing characters '*' and '?' are supported, so you can specify a pattern
                             that will be matched against the actual remote user name.
                             If any user was allowed, use '--user *' (you might need to escape '*' from your shell)
  --port PORT|*            Remote port that was allowed to connect to
                             If any port was allowed, use '--port *' (you might need to escape '*' from your shell)
  --protocol PROTO         Specify that a special protocol allowance should be removed from this HOST:PORT tuple, note that you
                              must not specify --user in that case.
                              PROTO must be one of:
                              scpupload    allow SCP upload, you--bastion-->server
                              scpdownload  allow SCP download, you<--bastion--server
                              sftp         allow usage of the SFTP subsystem, through the bastion
                              rsync        allow usage of rsync, through the bastion
  --proxy-host HOST|IP     Specify which host was used as a proxy/jump host to reach the target server
  --proxy-port PORT        Proxy port that was used to reach the target server
  --proxy-user USER|PATTERN|*  Proxy user that was configured for this access (mandatory when --proxy-host is specified).
                               Globbing characters '*' and '?' are supported for pattern matching.
EOF
);

my $fnret;

if (!$ip) {
    help();
    osh_exit 'ERR_MISSING_PARAMETER', "Missing parameter 'host' or didn't resolve correctly";
}

my $proxyIp;
if ($proxyHost) {
    $fnret = OVH::Bastion::is_valid_ip(ip => $proxyHost, allowSubnets => 0);
    if ($fnret) {
        $proxyIp = $fnret->value->{'ip'};
    }
    else {
        $fnret = OVH::Bastion::get_ip(host => $proxyHost);
        if ($fnret) {
            $proxyIp = $fnret->value->{'ip'};
        }
        else {
            $proxyIp = $proxyHost;
        }
    }
}

if ($proxyUser) {
    $fnret = OVH::Bastion::is_valid_remote_user(user => $proxyUser, allowWildcards => 1);
    $fnret or osh_exit $fnret;
    $proxyUser = $fnret->value;
}

$fnret = OVH::Bastion::Plugin::ACL::check(
    user      => $user,
    userAny   => $userAny,
    port      => $port,
    portAny   => $portAny,
    scpUp     => $scpUp,
    scpDown   => $scpDown,
    sftp      => $sftp,
    protocol  => $protocol,
    proxyIp   => $proxyIp,
    proxyPort => $proxyPort,
    proxyUser => $proxyUser,
);
if (!$fnret) {
    help();
    osh_exit($fnret);
}
$user      = $fnret->value->{'user'};
$port      = $fnret->value->{'port'};
$proxyUser = $fnret->value->{'proxyUser'};

my @command = qw{ sudo -n -u allowkeeper -- /usr/bin/env perl -T };
push @command, $OVH::Bastion::BASEPATH . '/bin/helper/osh-accountModifyPersonalAccess';
push @command, '--target',     'self';
push @command, '--action',     'del';
push @command, '--account',    $self;
push @command, '--ip',         $ip;
push @command, '--user',       $user      if $user;
push @command, '--port',       $port      if $port;
push @command, '--proxy-ip',   $proxyIp   if $proxyIp;
push @command, '--proxy-port', $proxyPort if $proxyPort;
push @command, '--proxy-user', $proxyUser if $proxyUser;

osh_exit OVH::Bastion::helper(cmd => \@command);
